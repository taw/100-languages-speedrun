# rules for adding one to a digit ::=
# only 9 is special ::=
[0-8]ğŸ§²::=âœ…[1-9]
9ğŸ§²::=ğŸ§²0
# if number ended but extra carry arrived, add leading 1 ::=
^ğŸ§²::=^âœ…1
,ğŸ§²::=,âœ…1
# If we reach a comma that means another number starts ::=
# we also want to increment it so switch back to ğŸ§² mode ::=
,âœ…::=ğŸ§²,
# we're done, so just let âœ… pass through ::=
[0-9]âœ…::=âœ…[0-9]
# start the printer ::=
# it's simpler to look at both counter at once ::=
# we reset the counters, then start printer in proper mode ::=
^âœ…::=^ğŸ“†
ğŸ“†1,1,::=1,1,ğŸ–¨
ğŸ“†2,1,::=2,1,ğŸ–¨
ğŸ“†3,1,::=0,1,ğŸ™ğŸ“„F
ğŸ“†1,2,::=1,2,ğŸ–¨
ğŸ“†2,2,::=2,2,ğŸ–¨
ğŸ“†3,2,::=0,2,ğŸ™ğŸ“„F
ğŸ“†1,3,::=1,3,ğŸ–¨
ğŸ“†2,3,::=2,3,ğŸ–¨
ğŸ“†3,3,::=0,3,ğŸ™ğŸ“„F
ğŸ“†1,4,::=1,4,ğŸ–¨
ğŸ“†2,4,::=2,4,ğŸ–¨
ğŸ“†3,4,::=0,4,ğŸ™ğŸ“„F
ğŸ“†1,5,::=1,0,ğŸ™ğŸ“„B
ğŸ“†2,5,::=2,0,ğŸ™ğŸ“„B
ğŸ“†3,5,::=0,0,ğŸ™ğŸ“„X
# if we have ğŸ“„F, ğŸ“„B, or ğŸ“„X, we just print that ::=
# then we pass ğŸ™ to the right without printing number ::=
ğŸ“„F::=~Fizz
ğŸ“„B::=~Buzz
ğŸ“„X::=~FizzBuzz
ğŸ™[0-9]::=[0-9]ğŸ™
# print the result if we're in number print mode ::=
# first activate each letter with ğŸ“„, but leave a copy behind ::=
ğŸ–¨[0-9]::=[0-9]ğŸ–¨ğŸ“„[0-9]
# now print the activated letter ::=
ğŸ“„[0-9]::=~[0-9]
# once printing finished, switch to increment mode ğŸ§²
ğŸ–¨$::=ğŸğŸ§²$
ğŸ™$::=ğŸğŸ§²$
ğŸ::=~
::=
^0,0,0ğŸ§²$
